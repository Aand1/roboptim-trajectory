# Don't expend macros beginning by JRL_.
m4_pattern_forbid([^JRL_])
AC_PREREQ([2.59])

## --------------------- ##
## Package configuration ##
## --------------------- ##
AC_INIT([RobOptim trajectory library], [0.1],
        [roboptim-user@lists.sourceforge.net], [roboptim-trajectory])

# Auxiliary files.
AC_CONFIG_AUX_DIR([build-aux])

# Automake.
AM_INIT_AUTOMAKE([1.9.6 dist-bzip2 -Wall nostdinc])

## ------------- ##
## C++ compiler  ##
## ------------- ##

# Search for a C++ compiler.
AC_LANG([C++])
AC_PROG_CXX

# Speed-up compilation.
if test "$GXX" = yes; then
  CXXFLAGS="$CXXFLAGS -pipe"
fi

# Customize warnings.
JRL_CXX_WARNINGS([[-Wall],
                   [-Werror],
                   [-Wcast-align],
                   [-Wcast-qual],
                   [-Wformat],
                   [-Wmissing-prototypes],
                   [-Wstrict-prototypes],
                   [-Wwrite-strings],
                   [-Wbad-function-cast],
                   [-Wmissing-declarations],
                   [-Wnested-externs]])

## ------------------- ##
## Additionnal tools.  ##
## ------------------- ##

# Search for misc. common tools.
AC_PROG_MKDIR_P
AC_PROG_SED

AC_ARG_VAR([DOXYGEN], [the Doxygen documentation generation program])
AC_CHECK_PROGS([DOXYGEN], [doxygen])

# Search for Libtool.
AC_CONFIG_MACRO_DIR([build-aux])
AC_PROG_LIBTOOL

# Search for Boost
# FIXME: check for Boost here.

# Search for RobOptim core.
ROBOPTIM_CORE_LIB
ROBOPTIM_CORE_HEADERS(,[roboptim_core_found=no])
if test x$roboptim_core_found = xno; then

	m4_define([roboptim_core_reqd], [roboptim-core >= 0.1])
	AC_SUBST([PACKAGE_REQD], ["roboptim_core_reqd"])
	PKG_CHECK_MODULES([ROBOPTIMCORE], [roboptim_core_reqd])

	ROBOPTIMCORE_DOCDIR=`$PKG_CONFIG roboptim-core --variable=docdir`
	AC_SUBST([PACKAGE_REQD])
	AC_SUBST([ROBOPTIMCORE_DOCDIR])
	AC_SUBST([ROBOPTIMCORE_CFLAGS])
	AC_SUBST([ROBOPTIMCORE_LIBS])
fi

## ------------ ##
## Test-suite.  ##
## ------------ ##

AC_CONFIG_TESTDIR([tests])
AM_MISSING_PROG([AUTOM4TE], [autom4te])


## -------------- ##
## Write output.  ##
## -------------- ##

# Create config.h
AC_CONFIG_HEADERS([config.h])

# Create Makefiles.
AC_CONFIG_FILES([
  Makefile
   include/Makefile
   src/Makefile
   doc/Makefile
   tests/Makefile
    tests/atlocal
    tests/package.m4
])

AC_CONFIG_FILES([doc/Doxyfile])

AC_CONFIG_FILES([doc/sf-upload.sh], [chmod +x doc/sf-upload.sh])

# Write files.
AC_OUTPUT
